#Fixed by Dub.
siamese_missions_1 = {
	slot = 1
	generic = no
	ai = yes
	potential = {
		OR = {
			tag = AYU
			tag = SIA
		}
		NOT = { map_setup = map_setup_random }
	}
	has_country_shield = yes
	
	ayu_taification = {
		icon = mission_have_two_subjects
		required_missions = {  }
		completed_by = 1500.1.1
		provinces_to_highlight = {
			province_id = 8567
		}
		trigger = {
			8567 = { #Seems these provinces were moved to 8500s. Changing to 8567.
				religion = ROOT
			}
		}
		effect = {
			central_thai_area = {
				limit = {
					is_core = ROOT
				}
				change_culture = ROOT
			}
			prachin_area = {
				limit = {
					is_core = ROOT
				}
				change_culture = ROOT
			}
			cambodia_area = {
				limit = {
					is_core = ROOT
				}
				change_culture = ROOT
			}
			khorat_area = {
				limit = {
					is_core = ROOT
				}
				change_culture = ROOT
			}
			#Claims
			northern_thai_area = {
				limit = {
					NOT = { is_core = ROOT }
					NOT = { is_permanent_claim = ROOT }
				}
				add_permanent_claim = ROOT
			}
		}
	}
	
	ayu_subjugate_lan_na = {
		icon = mission_build_up_to_force_limit
		required_missions = { ayu_taification }
		position = 5
		completed_by = 1605.1.1
		provinces_to_highlight = {
			area = northern_thai_area
			NOT = {
				country_or_non_sovereign_subject_holds = ROOT
			}
		}
		trigger = {
			northern_thai_area = {
				type = all
				owned_by = ROOT
			}
		}
		effect = {
			cambodia_area = {
				limit = {
					NOT = { is_core = ROOT }
					NOT = { is_permanent_claim = ROOT }
				}
				add_permanent_claim = ROOT
			}
			lower_burma_area = {
				limit = {
					NOT = { is_core = ROOT }
					NOT = { is_permanent_claim = ROOT }
				}
				add_permanent_claim = ROOT
			}
		}
	}
	ayu_conquer_the_khmer_remenants = {
		icon = mission_unite_home_region
		required_missions = { ayu_subjugate_lan_na }
		completed_by = 1779.1.1
		provinces_to_highlight = {
			area = cambodia_area
			NOT = {
				country_or_non_sovereign_subject_holds = ROOT
			}
		}
		trigger = {
			cambodia_area = {
				type = all
				owned_by = ROOT
			}
		}
		effect = {
			add_prestige = 25
			add_mil_power = 100
		}
	}
}

siamese_missions_2 = {
	slot = 2
	generic = no
	ai = yes
	potential = {
		OR = {
			tag = AYU
			tag = SIA
		}
		NOT = { map_setup = map_setup_random }
	}
	has_country_shield = yes
	ayu_reclaim_hanthawaddy = {
		icon = mission_conquer_siam
		required_missions = { ayu_subjugate_lan_na }
		position = 6
		provinces_to_highlight = {
			area = lower_burma_area
			NOT = {
				country_or_non_sovereign_subject_holds = ROOT
			}
		}
		trigger = {
			lower_burma_area = {
				type = all
				country_or_non_sovereign_subject_holds = ROOT
			}
		}
		effect = {
			add_prestige = 25
			add_adm_power = 100
		}
	}
}